commands                    = command+

command                     = function
                            | object


function                    = fixture_selector set_function
                            | "home" [homeable_object]
                            | "record" record_function
                            | "create" create_function
                            | "rename" rename_function
                            | "update" update_function
                            | "delete" delete_function "really"
                            | "clear"
                            | "nuzul" | "sueud" | "grandetc"
                            | "save"
                            | "config" ("output" | "patch")
                            | "test" string
                            | "assign" assign_command
                            | "unassign" unassign_command
                            | "recall" recall_command
                            | "lock"

recall_command              = "sequence" integer "cue" cue_idx

unassign_command            = "button" float
                            | "fader" float

assign_function             = "executor" integer ("go" | "stop" | "flash" | "fader") "to" float 
                            | "preset" integer ["with" fixture_selector] "to" float
                            | fixture_selector "to" float
                            | "macro" function "to" float
                            | "tokens" "to" float "with" [*]
                            | "grandmaster" "to" float
                            | "speedmaster" integer ("fader" | "tap") "to" float

delete_function             = "macro" integer_or_range
                            | sepcific_preset_or_range
                            | "sequence" integer_or_range
                            | "executor" integer_or_range
                            | "group" integer_or_range

update_function             = specific_preset "for" fixture_selector [udpate_mode]
                            | ("sequence" | "executor") integer "cue" cue_idx "for" fixture_selector ["with" (channel_type_list | "active" | "all")] [update_mode]

rename_function             = specific_preset "to" string
                            | "group" integer "to" string
                            | "sequence" integer "to" string


record_function             = "preset" feature_group (integer | "next") "for" fixture_selector ["as" string] ["next"]
                            | "group" (integer | "next") "for" fixture_selector ["as" string]
                            | "sequence" integer "cue" cue_idx "for" fixture_selector ["with" (channel_type_list | "active" | "all")]
                            | "executor" integer ["cue" cue_idx] "for" fixture_selector ["with" (channel_type_list | "active" | "all")] ["as" string]

create_function             = "sequence" (integer | "next") ["as" string]
                            | "executor" (integer | "next") "for" ("sequence" integer)
                            | "macro" (integer | "next") "with" command ["as" string]
                            | "preset" float ["as" string]

set_function                = channel_type value_single
                            | specific_preset_or_range

object_range                = object ["thru" object]

object                      = homeable_object
                            | specific_preset
                            | "sequence" integer ["cue" cue_idx]
                            | "macro" integer

homeable_object             = fixture_selector
                            | "executor" integer

feature_group               = "intensity"
                            | "color"
                            | "position"
                            | "beam"
                            | "focus"
                            | "control"
                            | "feature" integer

specific_preset             = "preset" integer
specific_preset_or_range    = specific_preset ["thru" specific_preset]

value_single                = discrete_value_single
                            | discrete_value_single "thru" discrete_value_single

discrete_value_single       = float
                            | "full"
                            | "half"
                            | "out"

channel_type_list           = "(" channel_type ("," channel_type)* ")"

channel_type                = discrete_channel_type
                            | "maintenance" string

discrete_channel_type       = "intensity"
                            | "color"
                            | "position"
                            | "strobe"

update_mode                 = "merge"
                            | "override"

fixture_selector            = atomic_fixture_selector
                            | atomic_fixture_selector "+" atomic_fixture_selector
                            | atomic_fixture_selector "-" atomic_fixture_selector
                            | atomic_fixture_selector "%" integer

atomic_fixture_selector     = integer
                            | integer "thru" integer
                            | "group" integer
                            | "(" fixture_selector ")"
                            | "~"

integer_or_range            = integer ["thru" integer]

cue_idx                     = discrete_cue_idx
                            | "next"

discrete_cue_idx            = integer | float

integer                     = digit+
float                       = digit+ ["." digit+]
digit                       = "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" | "0"

string                      = '"' letter+ '"'
letter                      = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"
                            | "~" | "@" | "#" | "%" | "_" | "$" | "&" | "'" | "-" | "+" | "/" | " "
                            | digit