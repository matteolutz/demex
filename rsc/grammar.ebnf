commands                    = command+

command                     = function
                            | object


function                    = fixture_selector set_function
                            | "home" [homeable_object]
                            | "record" record_function
                            | "create" create_function
                            | "rename" rename_function
                            | "update" update_function
                            | "delete" delete_function "really"
                            | "clear"
                            | "nuzul" | "sueud"
                            | "save"
                            | "config" ("output")

delete_function             = "macro" integer_or_range

update_function             = "preset" discrete_channel_type integer "for" fixture_selector [udpate_mode]

renane_function             = "preset" discrete_channel_type integer "to" string
                            | "group" integer "to" string
                            | "sequence" integer "to" string


record_function             = "preset" discrete_channel_type integer "for" fixture_selector ["as" string]
                            | "group" integer "for" fixture_selector ["as" string]
                            | "sequence" integer "cue" cue_idx "for" fixture_selector ["with" (channel_type_list | "active" | "all")]

create_function             = "sequence" integer ["as" string]
                            | "executor" integer "for" ("sequence" integer) ["as" string]
                            | "macro" integer "with" command ["as" string]

set_function                = channel_type value_single
                            | specific_preset_or_range

object                      = homeable_object
                            | specific_preset
                            | "sequence" integer ["cue" cue_idx]

homeable_object             = fixture_selector
                            | "executor" integer
                            | "fader" integer

specific_preset             = "preset" discrete_channel_type integer
specific_preset_or_range    = specific_preset ["thru" integer]

value_single                = discrete_value_single
                            | discrete_value_single "thru" discrete_value_single

discrete_value_single       = float
                            | "full"
                            | "half"
                            | "out"

channel_type_list           = "(" channel_type ("," channel_type)* ")"

channel_type                = discrete_channel_type
                            | "maintenance" string

discrete_channel_type       = "intensity"
                            | "color"
                            | "position"
                            | "strobe"

update_mode                 = "merge"
                            | "override"

fixture_selector            = atomic_fixture_selector
                            | atomic_fixture_selector "+" atomic_fixture_selector
                            | atomic_fixture_selector "-" atomic_fixture_selector
                            | atomic_fixture_selector "%" integer

atomic_fixture_selector     = integer
                            | integer "thru" integer
                            | "group" integer
                            | "(" fixture_selector ")"
                            | "~"

integer_or_range            = integer ["thru" integer]

cue_idx                     = discrete_cue_idx
                            | "next"

discrete_cue_idx            = integer | float

integer                     = digit+
float                       = digit+ ["." digit+]
digit                       = "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" | "0"

string                      = '"' letter+ '"'
letter                      = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"
                            | "~" | "@" | "#" | "%" | "_" | "$" | "&" | "'" | "-" | "+" | "/" | " "
                            | digit